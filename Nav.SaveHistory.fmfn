/*
 *  CUSTOM FUNCTION
 *    Nav.SaveHistory
 *
 *  DESCRIPTION
 *    Adds the current layout to the current list of layout options.
 *
 *  PARAMS
 *    --NONE--
 *
 *  RETURNS
 *    Sets an updated JSON to $$nav_history in the following format:
 *      {
 *          <<windowName>> : {
 *              "currentIndex" : x,
 *              "history" [
 *                  {
 *                      "name" : "",
 *                      "number" : x,
 *                  }
 *              ]
 *          }
 *      }
 *
 *  DEPENDENCIES
 *    VAR: $$nav_history
 *
 *  CHANGELOG
 *    2020-04-12: Paul McCudden (paul@peak14.solutions): Created
*/

Let (
  [
    //Initial Variables
    ~wName = GetValue ( Get ( WindowName ); 1 );
    ~thisHistory = JSONGetElement ( $$nav_history ; ~wName );
    ~thisHistoryExists = GetAsBoolean ( Left ( ~thisHistory ; 1 ) <> "?" );
    $$nav_history = If ( ~thisHistoryExists ; $$nav_history ; JSONSetElement ( $$nav_history ; ~wName & ".currentIndex" ; -1 ; JSONNumber ) ) ;

    ~lName = Get ( LayoutName );
    ~lNumber = Get ( LayoutNumber );

    ~thisItem = JSONSetElement ( "" ;
      ["name" ; ~lName ; JSONString];
      ["number" ; ~lNumber ; JSONNumber]
    );

    ~currentIndex = JSONGetElement ( $$nav_history ; ~wName & ".currentIndex" );
    ~wentBack = JSONGetElement ( $$nav_history ; ~wName & ".history" & "[" & ~currentIndex - 1 & "]" ) = ~thisItem;
    ~noMove = JSONGetElement ( $$nav_history ; ~wName & ".history" & "[" & ~currentIndex & "]" ) = ~thisItem;
    ~newIndex = ~currentIndex + If ( ~wentBack ; -1 ; 1 );

    $$nav_history = If ( ~noMove ; $$nav_history ; JSONFormatElements ( JSONSetElement ( $$nav_history ;
      [~wName & ".history[" & ~newIndex & "]"; ~thisItem; JSONObject];
      [~wName & ".currentIndex"; ~newIndex ; JSONNumber]
    ) ) )
  ] ;

  //Result for hide logic
  False

)