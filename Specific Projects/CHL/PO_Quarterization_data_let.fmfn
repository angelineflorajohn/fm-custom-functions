Let (
    [
        ~json = "{}";

        // Actuals - Invoices
        ~json = While (
            [
                ~json = ~json; // Needed due to a bug that will clear ~json if this loop doesn't run
                ~i = 0;
                ~count = Count ( INVOICE__PO::ID );
                ~thisKey = "actuals"
            ];

            ~i < ~count;

            [
                ~i = ~i + 1;
                ~thisFYQ = GetNthRecord ( INVOICE__PO::FYQ; ~i );
                ~thisFY = GetNthRecord ( INVOICE__PO::FY; ~i );
                ~thisAmount = GetNthRecord ( INVOICE__PO::TOTAL_USD; ~i );
                ~currentFYQAmount = JSONGetElement ( ~json; ~thisFYQ & "." & ~thisKey );
                ~currentFYAmount = JSONGetElement ( ~json; ~thisFY & "." & ~thisKey );
                ~json = JSONSetElement ( ~json
                    ;[~thisFYQ & "." & ~thisKey; ~currentFYQAmount + ~thisAmount; JSONNumber]
                    ;[~thisFY & "." & ~thisKey; ~currentFYAmount + ~thisAmount; JSONNumber]
                )

            ];

            ~json

        );


        // Planned - Engineer_Salaries
        ~json = While (
            [
                ~json = ~json; // Needed due to a bug that will clear ~json if this loop doesn't run
                ~i = 0;
                ~count = Count ( ENGINEER_SALARY_QUARTERLY__PO::ID );
                ~thisKey = "planned"
            ];

            ~i < ~count;

            [
                ~i = ~i + 1;
                ~thisFYQ = GetNthRecord ( ENGINEER_SALARY_QUARTERLY__PO::FYQ; ~i );
                ~thisFY = GetNthRecord ( ENGINEER_SALARY_QUARTERLY__PO::FY; ~i );
                ~thisAmount = GetNthRecord ( ENGINEER_SALARY_QUARTERLY__PO::SALARY_FOR_QUARTER; ~i );
                ~currentFYQAmount = JSONGetElement ( ~json; ~thisFYQ & "." & ~thisKey );
                ~currentFYAmount = JSONGetElement ( ~json; ~thisFY & "." & ~thisKey );
                ~json = JSONSetElement ( ~json
                    ;[~thisFYQ & "." & ~thisKey; ~currentFYQAmount + ~thisAmount; JSONNumber]
                    ;[~thisFY & "." & ~thisKey; ~currentFYAmount + ~thisAmount; JSONNumber]
                )

            ];

            ~json

        )
    ];

    ~json

)