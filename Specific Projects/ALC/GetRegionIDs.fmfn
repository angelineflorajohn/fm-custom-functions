/*
 *  CUSTOM FUNCTION
 *    GetRegionIDs ( )
 *
 *  DESCRIPTION
 *    Gets a list of the region IDs based off the current user
 *
 *  PARAMS
 *    --NONE--
 *
 *  RETURNS
 *    Text
 *
 *  DEPENDENCIES
 *    --NONE--
 *
 *  CHANGELOG
 *    2022-04-05: Paul McCudden: Created
*/

While (
  [
    ~regionNames = Substitute ( ExecuteSQL (
        "SELECT " & SqField ( USER::REGIONS ) &
        "¶FROM " & SqTable ( USER::ID ) &
        "¶WHERE " & SqField ( USER::ACCOUNT ) & " = '" & Get ( AccountName ) & "'";
        ""; ""
    ); ", "; ¶ );
    ~regionIDList = "";
    ~count = ValueCount ( ~regionNames );
    ~i = 0
  ];

  ~i < ~count;

  [
      ~i = ~i + 1;
      ~thisRegionName = GetValue ( ~regionNames; ~i );
      ~thisRegionID = ExecuteSQL (
          "SELECT" & SqField ( REGIONS::_ ) &
          "¶FROM " & SqTable ( REGIONS::ID ) &
          "¶WHERE " & SqField ( REGIONS::_ ) & " = '" & ~thisRegionName & "'";
          ""; ""
      );
      ~regionIDList = List ( ~regionIDList; ~thisRegionID )
  ];

  ~regionIDList

)