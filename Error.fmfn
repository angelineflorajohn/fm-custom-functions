 /*
 *  CUSTOM FUNCTION
 *    Error ( theErrorType ; theErrorCode ; theErrorDescription ; theErrorInfo )
 *
 *  DESCRIPTION
 *		This function is used to gather environmental data. You may choose
 *		to add or remove name/value pairs from this function as you see fit
 *		for your solution. All error generating custom functions call this
 *		function, so it is the central place to define default environmental
 *		data collected when an error occurs.
 *
 *		Environmental data that is likely to change from the time the error occurs
 *		to the time log data is collected should be included in this function.
 *
 *  PARAMS
 *    errorType: origin of the error
 *			(e.g., "Fmp", "App", "Plugin: ScriptMaster", "mFM: JSON", etc.)
 *		errorNumber: the number/code of the error
 *		errorDescription = (text) description of the error, preferably human readable
 *		errorInfo: some text describing the particular instance of this error. 
 *      This should help a developer locate where
 *			in a script the error occured.
 *			(e.g., "create new invoice record")
 *
 *  RETURNS
 *    JSON containing information about the error and the environment it occurred in.
 *
 *  DEPENDENCIES
 *    --NONE--
 *
 *  CHANGELOG
 2020-03-19: Paul McCudden (paul@peak14.solutions): Created
 *    2013-10-11: Daniel Smith (dansmith65@gmail.com): Created
 *    2013-11-22: Daniel Smith (dansmith65@gmail.com):
 *			- change name to Error, update documentation
 *    2018-06-06: Mick Crozier (mick@backstagesolutions.com.au):
 *      - Completely revamped for JSON rather than Let Notation
 *    2018-11-15: Paul McCudden (paul@peak14.solutions):
 *      - Added comprehensive system data
 *    2020-04-28: Paul McCudden (paul@peak14.solutions):
 *      - Changed to use while loop for easier readability
 *
 * REFERENCES:
 *		http://filemakerstandards.org/x/AoA-/
*/

While ( 
  [
    ~i = 0 ;
    ~stateJson = "" ;
    ~stateList = List (


      //<><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><>
      // Comment or uncomment what you'd like to recieve here.
      //<><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><>

      //-- High priority values moved to top for easy viewing --
      "AccountName" ;
      "CurrentHostTimestamp" ;
      "CurrentTimestamp" ;
      "Device" ;
      "FileName" ;
      "ScriptName" ;
      "ScriptParameter" ;
      "ScriptResult" ;

      //-- Lower priority items --
      "AccountExtendedPrivileges" ;
      "AccountPrivilegeSetName" ;
      "ActiveFieldContents" ;
      "ActiveFieldName" ;
      "ActiveFieldTableName" ;
      "ActiveLayoutObjectName" ;
      "ActiveModifierKeys" ; 
      //"ActiveRepetitionNumber" ;
      //"ActiveSelectionSize" ;
      //"ActiveSelectionStart" ;
      //"AllowFormattingBarState" ;
      "AllowAbortState" ;
      "ApplicationLanguage" ;
      "ApplicationVersion" ;
      //"CalculationRepetitionNumber" ;
      //"ConnectionAttributes" ;
      "ConnectionState" ;
      "CurrentDate" ;
      "CurrentTime" ;
      "CustomMenuSetName" ;
      //"DesktopPath" ;
      //"DocumentsPath" ;
      //"DocumentsPathListing" ;
      "EncryptionState" ;
      "ErrorCaptureState" ;
      //"FileMakerPath" ;
      "FilePath" ;
      "FileSize" ;
      "FoundCount" ;
      //"HighContrastColor" ;
      //"HighContrastState" ;
      "HostApplicationVersion" ;
      "HostIPAddress" ;
      "HostName" ;
      "InstalledFMPlugins" ;
      "LastError" ;
      "LastExternalErrorDetail" ;
      "LastMessageChoice" ;
      "LastODBCError" ;
      "LayoutAccess" ;
      //"LayoutCount" ;
      "LayoutName" ;
      "LayoutNumber" ;
      "LayoutTableName" ;
      "LayoutViewState" ;
      "MultiUserState" ;
      "NetworkProtocol" ;
      "NetworkType" ;
      //"PageNumber" ;
      //"PreferencesPath" ;
      //"PrinterName" ;
      //"QuickFindText" ;
      "RecordAccess" ;
      "RecordID" ;
      //"RecordModificationCount" ;
      //"RecordNumber" ;
      "RecordOpenCount" ;
      "RecordOpenState" ;
      //"RequestCount" ;
      //"RequestOmitState" ;
      //"ScreenDepth" ;
      //"ScreenWidth" ;
      //"ScriptAnimationState" ;
      "SortState" ;
      "StatusAreaState" ;
      "SystemDrive" ;
      "SystemIPAddress" ;
      "SystemLanguage" ;
      //"SystemNICAddress" ;
      "SystemPlatform" ;
      "SystemVersion" ;
      //"TemporaryPath" ;
      //"TextRulerVisible" ;
      "TotalRecordCount" ;
      //"TriggerGestureInfo" ;
      //"TriggerKeystroke" ;
      //"TriggerCurrentPanel" ;
      "TriggerModifierKeys" ;
      //"TriggerTargetPanel" ;
      "UserCount" ;
      "UserName" ;
      //"UseSystemFormatsState" ;
      //"WindowContentHeight" ;
      //"WindowContentWidth" ;
      //"WindowDesktopHeight" ;
      //"WindowDesktopWidth" ;
      //"WindowHeight" ;
      //"WindowLeft" ;
      "WindowMode" ;
      "WindowName" ;
      //"WindowOrientation" ;
      "WindowStyle" ;
      //"WindowTop" ;
      "WindowVisible" ;
      //"WindowWidth" ;
      "WindowZoomLevel"
    ) 
  ] ; 

   ~i < ValueCount ( ~stateList ) ;

   [
    ~i = ~i + 1 ;
    ~thisItem = GetValue ( ~stateList ; ~i ) ;
    ~stateJSON = JSONSetElement ( ~stateJSON ;
      [~thisItem ; Evaluate ( "Get ( " & ~thisItem & " )" ) ; JSONString]
    )
  ];

  JSONSetElement ( "{}" ;
    [ "errorType" ; theErrorType ; JSONString];
    [ "errorNum" ; theErrorCode ; JSONNumber];
    [ "errorDescription" ; theErrorDescription ; JSONString];
    [ "errorInfo" ; theErrorInfo ; JSONString];
    [ "systemState"; ~stateJSON; JSONObject]
  )

)