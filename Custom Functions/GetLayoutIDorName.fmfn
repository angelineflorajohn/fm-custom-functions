/*
 *  CUSTOM FUNCTION
 *    GetLayoutIDorName ( layout )
 *
 *  DESCRIPTION
 *    Get the corresponding ID or layout name of the passed layout
 *
 *  PARAMS
 *    layout: The layout name or ID to convert
 *
 *  RETURNS
 *    Returns a string
 *
 *  DEPENDENCIES
 *    --None--
 *
 *  CHANGELOG
 *    2018-04-19: Paul McCudden (paul@peak14.solutions): Created
*/

Let ( 
   [ 
      ~layout = If ( IsEmpty ( layout ); Get ( LayoutName ); layout );

      //Lists of all layouts
      ~ids = LayoutIDs ( Get ( FileName ) ) ; 
      ~names = LayoutNames ( Get ( FileName ) ) ;
      
      //New Variable 
      ~newLayout =
         If ( Length ( GetAsNumber ( ~layout ) ) = Length ( ~layout ) ;
            
            //Entered Value is a Number
            MiddleValues ( ~names ;
               PatternCount (
                  Middle ( ~ids ; 1 ; 
                     Position ( ~ids ; ~layout ; 1 ; 1 )
                  ) ;
               "¶") + 1 ;
            1) ;

            //Entered Value is not a Number (text)
            MiddleValues ( ~ids ;
               PatternCount (
                  Middle ( ~names ; 1 ; 
                     Position ( ~names ; ~layout ; 1 ; 1 )
                  ) ;
               "¶") + 1 ;
            1) 
         )
   ];

   If ( FilterValues ( ~ids ; ~layout ) <> "" or FilterValues ( ~names ; ~layout ) <> "" ;
      Substitute ( ~newLayout ; ¶ ; "" ) ;
      ""
   )
)