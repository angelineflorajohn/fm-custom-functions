/*
 *  CUSTOM FUNCTION
 *    GetFrontPanels ( objectNameList )
 *
 *  DESCRIPTION
 *    Use to find the name(s) of the current front panels. If objectNameList is null, will use all object names on layout
 *
 *  PARAMS
 *    --NONE--
 *
 *  RETURNS
 *    A list of all objects that front panels
 *
 *  DEPENDENCIES
 *    --None--
 *
 *  CHANGELOG
 *    2020-03-04: Paul McCudden (paul@peak14.solutions): Created
*/

While (
  //initialVariable
  [
    ~unique = Get ( UUID );
    ~objectList = If ( IsEmpty ( objectNameList ) ; Substitute ( ~unique & LayoutObjectNames ( "" ; Get ( LayoutName ) ) & ~unique ; 
      ["<" ; ""];
      [">" ; ""];

      // Get rid of empty values
      [ "¶¶"; ¶ & ~unique & ¶ ];
      [ "¶¶"; ¶ & ~unique & ¶ ];
      [ ~unique & ¶; "" ];
      [ ¶ & ~unique; "" ];
      [ ~unique; "" ]
    ); objectNameList ) ;
    ~i = 0;
    ~count = ValueCount ( ~objectList );
    ~frontList = ""
  ]; 

  //condition
  ~i < ~count;

  //logic
  [
    ~i = ~i + 1;
    ~thisObject = GetValue ( ~objectList ; ~i );
    ~frontList = List ( ~frontList ; If ( GetLayoutObjectAttribute ( ~thisObject ; "isFrontPanel" ) ; ~thisObject ) )
  ]; 

  //result
  ~frontList

)
